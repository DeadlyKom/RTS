
                ifndef _CORE_MODULE_UTILS_CHUNK_ARRAY_INSERT_
                define _CORE_MODULE_UTILS_CHUNK_ARRAY_INSERT_
; -----------------------------------------
; вставка значения в массив чанков
; In:
;   A  - порядковый номер чанка [0..127]
;   H  - старший адрес массива чанков
;   D  - добавляемое значение
;   E  - количество элементов в массиве
; Out:
; Corrupt:
; Note:
; -----------------------------------------
Insert:         ; расчёт начальных адресов расположения юнитов в указаном чанке
                CALL GetAdrUnits

                ; HL - начальный адрес счётчиков юнитов в указанном чанке
                ; B  - обнулён
                ; A  - количествой пройденых элементов/начальный адрес расположения элементов

                ; увеличить счётчик
                INC (HL)

                ; HL - начальный адрес расположения элементов в указанном чанке
                LD L, A

                ; сохранение добовляемое значение
                LD A, D
                EX AF, AF'

                ; проверка 0 элемента
                LD A, E
                OR A
                JR Z, .Set

                ; перенос данных
                SUB L
                LD C, A
                ADD HL, BC
                LD D, H
                LD E, L
                DEC L
                LDDR
                INC HL

.Set            ; вставка значения
                EX AF, AF'
                LD (HL), A

                RET

                endif ; ~ _CORE_MODULE_UTILS_CHUNK_ARRAY_INSERT_
