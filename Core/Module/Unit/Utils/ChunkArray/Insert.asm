
                ifndef _CORE_MODULE_UTILS_CHUNK_ARRAY_INSERT_
                define _CORE_MODULE_UTILS_CHUNK_ARRAY_INSERT_
; -----------------------------------------
; вставка значения в массив чанков
; In:
;   A  - порядковый номер чанка [0..127]
;   HL - адрес массива чанков
;   D  - добавляемое значение
;   E  - количество элементов в массиве
; Out:
; Corrupt:
; Note:
; -----------------------------------------
Insert:         ; получение начального адреса чанка
                CALL GetChunkAdr

                ; -----------------------------------------
                ; Out:
                ;   HL - начальный адрес чанка
                ;   B  - обнулён
                ; -----------------------------------------

                ; сохранение добовляемое значение
                LD A, D
                EX AF, AF'

                ; перемещение данных (расширение) expand
                LD A, E

                ; -----------------------------------------
                ; In:
                ;   A  - количество элементов в массиве
                ;   HL - начальный адрес буфера перемещения
                ;   B  - обнулён
                ; -----------------------------------------
                
                INC L
.MapSizeChunks  EQU $+1
                ADD A, #00
                SUB L
                LD C, A
                ADD HL, BC
                LD D, H
                LD E, L
                DEC L
                LDDR

                ; -----------------------------------------
                ; Out:
                ;   HL - адрес счётчика элементов
                ;   DE - адрес пустого элемента 
                ; -----------------------------------------

                ; вставка значения
                INC (HL)
                EX AF, AF'
                LD (DE), A

                RET

                endif ; ~ _CORE_MODULE_UTILS_CHUNK_ARRAY_INSERT_
