
                ifndef _CORE_MODULE_UTILS_CHUNK_ARRAY_MOVE_
                define _CORE_MODULE_UTILS_CHUNK_ARRAY_MOVE_
; -----------------------------------------
; переместить значение в другой чанк
; In:
;   H  - старший байт адрес массива чанков (выровненый 256 байт)
;   D  - перемещяемое значение
;   B  - номер чанка источника  [0..127]
;   A' - номер чанка назначения [0..127]
; Out:
; Corrupt:
; Note:
; -----------------------------------------
Move:           ; определения прямой или обратное перемещение в массиве
                EX AF, AF'
                LD C, A
                JR NC, .IsForward

.IsReverse      ; обратное перемещение в массиве
                ; B - номер чанка источника  [0..127]
                ; С - номер чанка назначения [0..127]

                ; расчёт смещение от текущего адреса в массиве
                LD A, B
                SUB C
                LD E, A

                ; D  - перемещяемое значение
                ; E  - количество проходимых элементов
                ; B  - номер чанка источника  [0..127]
                ; C  - номер чанка назначения [0..127]

                ; расчёт начальных адресов расположения юнитов в указаном чанке
                LD L, #80
                LD A, C
                CALL GetAdrUnits

                ; HL - начальный адрес счётчиков юнитов в указанном чанке
                ; A  - количествой пройденых элементов/начальный адрес расположения элементов
                ; B  - обнулён

                ; увеличение счётчика элементов в новом чанке
                INC (HL)
                PUSH HL
                LD L, A

                ; сохранение искомого значения
                LD A, D
                EX AF, AF'
                
                ; копирование адреса
                LD A, E
                EX DE, HL
                POP HL

                ; расчёт конечных адресов расположения юнитов в указаном чанке
                SET 7, L
                CALL GetAdrUnits

                ; HL - начальный адрес счётчиков юнитов в указанном чанке
                ; A  - количествой пройденых элементов/начальный адрес расположения элементов
                ; B  - обнулён

                ; уменьшение счётчика элементов в старом чанке
                LD C, (HL)                                                      ; количество элементов в старом чанке
                DEC (HL)
                LD L, A
                JR Z, .L1

                PUSH DE
                LD A, C                                                         ; сохранение количество элементов в старом чанке
                DEC A

                ; поиск перемещяемого значения
                EX AF, AF'
                EX DE, HL
                CPIR
                DEBUG_BREAK_POINT_NZ                                            ; ошибка поиска!

                DEC L
                POP DE
                EX AF, AF'
                LD C, A

.L1             EX AF, AF'
                EX DE, HL
                LDDR

                ; сохранение перемещаемого значения
                INC HL
                LD (HL), A
                RET

.IsForward      ; прямое перемещение в массиве
                ; B - номер чанка источника  [0..127]
                ; С - номер чанка назначения [0..127]

                ; расчёт смещение от текущего адреса в массиве
                SUB B
                LD E, A

                ; D  - перемещяемое значение
                ; E  - количество проходимых элементов
                ; B  - номер чанка источника  [0..127]
                ; C  - номер чанка назначения [0..127]

                ; расчёт начальных адресов расположения юнитов в указаном чанке
                LD L, #80
                LD A, B
                CALL GetAdrUnits

                ; HL - начальный адрес счётчиков юнитов в указанном чанке
                ; A  - количествой пройденых элементов/начальный адрес расположения элементов
                ; B  - обнулён

                ; уменьшение счётчика элементов в новом чанке
                LD C, (HL)                                                      ; новое количество элементов в старом чанке
                PUSH HL
                LD L, A
                LD A, D
                EX AF, AF'
                LD A, C
                DEC A
                JR Z, .L2

                ; поиск перемещяемого значения
                LD A, D
                CPIR
                DEBUG_BREAK_POINT_NZ                                            ; ошибка поиска!
                
.L2             ;EX AF, AF'
                ;LD C, A


                ; копирование адреса
                LD A, E
                LD D, H
                LD E, L

                ; расчёт конечных адресов расположения юнитов в указаном чанке
                SET 7, L
                CALL GetAdrUnits

                EX (SP), HL
                LD C, (HL)
                DEC (HL)
                POP HL

                ; HL - начальный адрес счётчиков юнитов в указанном чанке
                ; A  - количествой пройденых элементов/начальный адрес расположения элементов
                ; B  - обнулён

                ; увеличение счётчика элементов в старом чанке
                INC (HL)
                ; LD E, C                                                         ; сохранение количество элементов в старом чанке
                LD L, A

                ; премещение блока
                
                ; DEC L
                ; LD C, E
                ; POP DE
                ; EX DE, HL
                ; LDDR
                LDIR

                ; сохранение перемещаемого значения
                DEC L
                EX AF, AF'
                LD (HL), A
                RET

                endif ; ~ _CORE_MODULE_UTILS_CHUNK_ARRAY_MOVE_
