
                ifndef _CORE_MODULE_UTILS_AABB_GET_RECT_
                define _CORE_MODULE_UTILS_AABB_GET_RECT_

                module AABB
; -----------------------------------------
; получить описывающий прямоугольник указанного юнита в пределах экрана
; In:
;   IX - указывает на структуру FUnit
; Out:
;   HL  - H - правый край спрайта,   L - левый край спрайта  (в пикселах)
;   DE  - D - верхний край спрайта,  E - нижний край спрайта (в пикселях)
;   если флаг переполнения C установлен, объект вне экрана
;   HL' - указывает на структуру текущего юнита FSprite.Dummy
; Corrupt:
;   HL, DE, BC, AF, HL', BC', AF'
; Note:
;   необходимо утановить 1 страничку хранения данных об юнитах
; -----------------------------------------
GetScreen:
.CurrentAddress ; сохраним предыдущие значения клипинга (если они были)
                CALL Sprite.SaveClip                                            
                PUSH BC
                
                ; быстрое отсечение видимости
                ; подготовка данных для следующих тестов
                CALL Sprite.FastClipping
                JR C, .RestoreClip

                ; получение адреса хранения информации о спрайте
                CALL Animation.SpriteInfo

                ; ---------------------------------------------
                LD A, #06                                                       ; дополнительная высота спрайта
                LD (Sprite.Clipping.Vertical.OffsetByPixel), A
                LD (Sprite.Clipping.Vertical.AddSizeByPixel), A
                CALL Sprite.Clipping.Vertical
                LD A, #00
                LD (Sprite.Clipping.Vertical.OffsetByPixel), A
                LD (Sprite.Clipping.Vertical.AddSizeByPixel), A
                JR C, .RestoreClip
                ; ---------------------------------------------
                ; L - хранит номер верхней линии спрайта    (в пикселях)
                ; C - высота видимой части спрайта          (в пикселях)
                ; ---------------------------------------------

                LD H, L
                LD A, L
                ADD A, C
                LD L, A
                ; ---------------------------------------------
                ; L - нижний край спрайта                   (в пикселах)
                ; H - верхний край спрайта                  (в пикселах)
                ; ---------------------------------------------

                PUSH HL

                ; ---------------------------------------------
                CALL Sprite.Clipping.Horizontal
                POP DE
                JR C, .RestoreClip
                ; ---------------------------------------------
                ; L - левый край спрайта                    (в пикселах)
                ; C - ширина видимой части спрайта          (в пикселах)
                ; ---------------------------------------------
                
                LD A, L
                ADD A, C
                LD H, A
                ; ---------------------------------------------
                ; L - левый край спрайта                    (в пикселах)
                ; H - правый край спрайта                   (в пикселах)
                ; ---------------------------------------------
                ; OR A

.RestoreClip:   POP BC
                JP Sprite.RestoreClip                                           ; востанови предыдущие значения клипинга (если они были)

                endmodule

                endif ; ~ _CORE_MODULE_UTILS_AABB_GET_RECT_