
                ifndef _CORE_MODULE_DRAW_SPRITE_UNIT_PREPARE_SPRITE_
                define _CORE_MODULE_DRAW_SPRITE_UNIT_PREPARE_SPRITE_

; -----------------------------------------
; подготовка спрайта перед выводм на экран
; In:
;   HL - адрес текущего спрайта
; Out:
; Corrupt:
; Note:
; -----------------------------------------
Prepare:        EX DE, HL
                
                ; инициализация
                LD HL, GameVar.TilemapOffset
                LD C, (HL)                                                      ; X
                INC HL
                LD B, (HL)                                                      ; Y
                
                EXX

.Clip           ; ---------------------------------------------
                ; вертикальный клипинг
                ; ---------------------------------------------
                ; DE' - изначальный/изменённый адрес спрайта
                ; ВС' - положение видимой области (B - y, C - x) в тайлах
                ; ---------------------------------------------

                ; размер спрайта (B - ширина, C - высота) в пикселях
.SpriteSize     EQU $+1
                LD BC, #0000

                ; -----------------------------------------
                ; расчёт положения спрайта по вертикали,
                ; относительно верхней границы видимой области
                ; -----------------------------------------

                ; приведение к форме хранения позиции юнита (12.4)
                ; SOy = FCompositeSpriteInfo.Info.OffsetY << 4
.SOy            EQU $+1
                LD L, #00
                LD A, L
                ADD A, A
                SBC A, A
                LD H, A
                LD A, L
                ADD A, A
                ADD A, A
                ADD A, A
                RL H
                ADD A, A
                RL H
                LD L, A

                ; HL = SOy + FUnit.Position.Y
.PositionY      EQU $+1
                LD DE, #0000
                ADD HL, DE

                ; преобразование высоты спрайта в 16-битное число
                LD D, #00
                LD A, C
                ADD A, A
                ADD A, A
                ADD A, A
                RL D
                ADD A, A
                RL D
                LD E, A

                ; HL = SOy + FUnit.Position.Y - GameVar.TilemapOffset.Y
                LD A, H
                EXX
                SUB B                                                           ; GameVar.TilemapOffset.Y
                EXX
                LD H, A                                                         ; HL - хранит положение спрайта, относительно верхней границы видимой области
                JP P, .BelowTop                                                 ; переход, если спрайт находится ниже верхней границы видимой области

.AboveTop       ; ---------------------------------------------
                ; спрайт выше границы видимой области
                ; ---------------------------------------------

                ; добавить к относительному расположению спрайта по вертикали,
                ; относительно верхней границы видимой области размер спрайта
                ADD HL, DE
                RET NC                                                          ; выход, если при добавлении размера спрайта по вертикали,
                                                                                ; не произошло переполнене. значение осталось отрицательным
                                                                                ; и спрайт полностью находится выше видимой области

.ClipTop        ; ---------------------------------------------
                ; спрайт урезан верхней частью видимой области
                ; ---------------------------------------------
                ; L   - новая высота спрайта
                ; DE  - 16-битное значение высоты спрайта
                ; BC  - изначальный размер спрайта (B - ширина, C - высота) в пикселях
                ; DE' - изначальный/изменённый адрес спрайта
                ; ВС' - положение видимой области (B - y, C - x) в тайлах
                ; ---------------------------------------------
                
                ; расчёт количество пропускаемых строк
                LD A, C
                SUB L
                LD C, L                                                         ; сохранение новой высоты спрайта
                
                ; расчёт адреса в таблицы умножения
                LD L, A
                LD A, B
                SUB #08                                                         ; начинается с 1
                ADD A, A
                ADD A, A
                DEC L                                                           ; начинается с 1
                XOR L
                AND %01100000
                XOR L

                ; результат:
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 0  | W1 | W0 | R4 | R3 | R2 | R1 | R0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   W1,W0   [6,5]   - ширина спрайта в знакоместах
                ;   R4-R0   [4..0]  - количество пропускаемых строк
                
                LD L, A
                LD H, HIGH Table.MultiplySprite
                LD A, (HL)

                ; корректировка размера пропускаемых данных спрайта,
                ; в зависимости от наличия маски у спрайта
                LD HL, GameFlags.SpriteFlagRef
                BIT CSIF_OR_XOR_BIT, (HL)
                JR Z, $+3
                ADD A, A

                EXX
                ; корректировка начального адреса спрайта
                ADD A, E
                LD E, A
                ADC A, D
                SUB E
                LD D, A

                ; адрес экранной области
                LD HL, Adr.Screens
                EXX

                JR .ClipRow

.ClipBottom     ; ---------------------------------------------
                ; спрайт урезан нижней частью видимой области
                ; ---------------------------------------------
                ; HL  - хранит положение спрайта, относительно нижней границы видимой области
                ; BC  - изначальный размер спрайта (B - ширина, C - высота) в пикселях
                ; DE' - изначальный/изменённый адрес спрайта
                ; ВС' - положение видимой области (B - y, C - x) в тайлах
                ; A   - положение спрайта, относительно верхней границы видимой области
                ; ---------------------------------------------

                ; приведение позиции спрайта 12.4 к 8-битному (экранноме значению)
                POP AF                                                          ; сброс значения в стеке
                ADD HL, HL
                ADD HL, HL
                ADD HL, HL
                ADD HL, HL
                LD A, H
                EX AF, AF'                                                      ; сохранение положение спрайта, относительно верхней границы видимой области
                
                ; корректировка высоты спрайта
                LD A, C
                SUB H
                LD C, A                                                         ; сохранение новой высоты спрайта

                ; проверка наличия маски у спрайта
                EXX
                LD HL, GameFlags.SpriteFlagRef
                BIT CSIF_OR_XOR_BIT, (HL)
                EXX
                JR Z, .NotMask                                                  ; переход, если у спрайта отсутствует маска

                ; расчёт адреса в таблицы умножения
                LD A, B
                SUB #08                                                         ; начинается с 1
                ADD A, A
                ADD A, A
                DEC H                                                           ; начинается с 1
                XOR H
                AND %01100000
                XOR H

                ; результат:
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 0  | W1 | W0 | R4 | R3 | R2 | R1 | R0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   W1,W0   [6,5]   - ширина спрайта в знакоместах
                ;   R4-R0   [4..0]  - количество пропускаемых строк
                
                LD L, A
                LD H, HIGH Table.MultiplySprite
                LD A, (HL)
                EXX
                LD B, A                                                         ; сохранение результата, для копирования спрайта с общей маской
                EXX

.NotMask        EX AF, AF'                                                      ; восстановление положение спрайта, относительно верхней границы видимой области
                JR .ScrAdrRow

.BelowTop       ; ---------------------------------------------
                ; спрайт находится ниже верхней границы видимой области
                ; ---------------------------------------------
                ; HL  - хранит положение спрайта, относительно верхней границы видимой области
                ; DE  - 16-битное значение высоты спрайта
                ; BC  - размер спрайта (B - ширина, C - высота) в пикселях
                ; DE' - изначальный/изменённый адрес спрайта
                ; ВС' - положение видимой области (B - y, C - x) в тайлах
                ; ---------------------------------------------

                ; проверка нахождения спрайта ниже нижней границы видивой области
                ; LD A, H
                SUB SCREEN_TILE_Y
                RET NC                                                          ; выход, если результат положительный, т.к. спрайт находится
                                                                                ; ниже нижней границы видимой области

                ; проверка урезания спрайта нижней частью видимой областью
                PUSH HL                                                         ; сохранение положение спрайта, относительно верхней границы видимой области (в пикселях)
                LD H, A
                ADD HL, DE
                JR C, .ClipBottom                                               ; переход, если при добавлении размера спрайта по вертикали,
                                                                                ; произошло переполнене. значение положительное
                                                                                ; спрайт урезан нижней границей видивой области



                ; приведение позиции спрайта 12.4 к 8-битному (экранноме значению)
                POP HL                                                          ; восстановление положение спрайта, относительно верхней границы видимой области (в пикселях)
                ADD HL, HL
                ADD HL, HL
                ADD HL, HL
                ADD HL, HL
                LD A, H

.ScrAdrRow      ; расчёт адреса строки в экранной области
                EXX
                LD H, HIGH Table.ScreenAddress
                LD L, A
                LD A, (HL)
                INC H
                LD H, (HL)
                LD L, A
                EXX

.ClipRow        ; ---------------------------------------------
                ; горизонтальный клипинг
                ; ---------------------------------------------
                ; BC  - размер спрайта (D - изначальная ширина, E - изначальная/изменённая высота) в пикселях
                ; HL' - адрес экрана вывода
                ; DE' - изначальный/изменённый адрес спрайта
                ; B'  - количество пропускаемых байт, для спрайтов с общей маской
                ; С'  - положение видимой области по горизонтали в тайлах
                ; ---------------------------------------------

                ; -----------------------------------------
                ; расчёт положения спрайта по горизонтали,
                ; относительно левой границы видимой области
                ; -----------------------------------------

                ; приведение к форме хранения позиции юнита (12.4)
                ; SOx = FCompositeSpriteInfo.Info.OffsetX << 4
.SOx            EQU $+1
                LD L, #00
                LD A, L
                ADD A, A
                SBC A, A
                LD H, A
                LD A, L
                ADD A, A
                ADD A, A
                ADD A, A
                RL H
                ADD A, A
                RL H
                LD L, A

                ; HL = SOx + FUnit.Position.X
.PositionX      EQU $+1
                LD DE, #0000
                ADD HL, DE

                ; преобразование ширины спрайта в 16-битное число
                LD D, #00
                LD A, B
                ADD A, A
                ADD A, A
                ADD A, A
                RL D
                ADD A, A
                RL D
                LD E, A

                ; HL = SOx + FUnit.Position.X - GameVar.TilemapOffset.X
                LD A, H
                EXX
                SUB C                                                           ; GameVar.TilemapOffset.X
                LD C, B                                                         ; копирование значение, для спрайтов с общей маской
                EXX
                LD H, A                                                         ; HL - хранит положение спрайта, относительно левой границы видимой области
                JP P, .ToRightOfEdge                                            ; переход, если спрайт находится правее левой границы видимой области

.ToLeftOfEdge   ; ---------------------------------------------
                ; спрайт левее границы видимой области
                ; ---------------------------------------------

                ; добавить к относительному расположению спрайта по горизонтали,
                ; относительно левой границы видимой области размер спрайта
                ADD HL, DE
                RET NC                                                          ; выход, если при добавлении размера спрайта по горизонтали,
                                                                                ; не произошло переполнене. значение осталось отрицательным
                                                                                ; и спрайт полностью находится левее видимой области

.ClipLeft       ; ---------------------------------------------
                ; спрайт урезан левой частью видимой области
                ; ---------------------------------------------
                ; L   - новая ширина спрайта
                ; DE  - 16-битное значение ширины спрайта
                ; BC  - размер спрайта (B - изначальная ширина, C - изначальная/изменённая высота) в пикселях
                ; HL' - адрес экрана вывода
                ; DE' - изначальный/изменённый адрес спрайта
                ; B'  - количество пропускаемых байт, для спрайтов с общей маской
                ; С'  - количество пропускаемых байт, для спрайтов с общей маской
                ; A   - положение спрайта, относительно левой границы видимой области
                ; ---------------------------------------------

                EX AF, AF'                                                      ; сохранение положение спрайта, относительно левой границы видимой области

                ; расчёт количество пропускаемых строк
                LD A, B
                SUB L
                LD B, L                                                         ; сохранение новой ширины спрайта

                EX AF, AF'                                                      ; восстановление положение спрайта, относительно левой границы видимой области
                JR .AdjustScrAdr

.ClipRight      ; ---------------------------------------------
                ; спрайт урезан правой частью видимой области
                ; ---------------------------------------------
                ; HL  - хранит положение спрайта, относительно правой границы видимой области
                ; BC  - размер спрайта (B - изначальная ширина, C - изначальная/изменённая высота) в пикселях
                ; HL' - адрес экрана вывода
                ; DE' - изначальный/изменённый адрес спрайта
                ; B'  - количество пропускаемых байт, для спрайтов с общей маской
                ; С'  - количество пропускаемых байт, для спрайтов с общей маской
                ; A   - положение спрайта, относительно левой границы видимой области
                ; ---------------------------------------------

                ; приведение позиции спрайта 12.4 к 8-битному (экранноме значению)
                POP AF                                                          ; сброс значения в стеке
                ADD HL, HL
                ADD HL, HL
                ADD HL, HL
                ADD HL, HL

                ; корректировка ширины спрайта
                LD A, H
                ADD A, B
                LD B, A                                                         ; сохранение новой ширины спрайта

                JR .AdjustScrAdr

.ToRightOfEdge  ; ---------------------------------------------
                ; спрайт находится правее левой границы видимой области
                ; ---------------------------------------------
                ; HL  - хранит положение спрайта, относительно левой границы видимой области
                ; DE  - 16-битное значение ширины спрайта
                ; BC  - размер спрайта (B - изначальная ширина, C - изначальная/изменённая высота) в пикселях
                ; HL' - адрес экрана вывода
                ; DE' - изначальный/изменённый адрес спрайта
                ; B'  - количество пропускаемых байт, для спрайтов с общей маской
                ; С'  - количество пропускаемых байт, для спрайтов с общей маской
                ; ---------------------------------------------

                ; проверка нахождения спрайта правее правой границы видивой области
                ; LD A, H
                SUB SCREEN_TILE_X
                RET NC                                                          ; выход, если результат положительный, т.к. спрайт находится
                                                                                ; правее правой границы видимой области

                ; проверка урезания спрайта правой частью видимой областью
                PUSH HL                                                         ; сохранение положение спрайта, относительно левой границы видимой области (в пикселях)
                LD H, A
                ADD HL, DE
                JR C, .ClipRight                                                ; переход, если при добавлении размера спрайта по горизонтали,
                                                                                ; произошло переполнене. значение положительное
                                                                                ; спрайт урезан правой границей видивой области

                ; приведение позиции спрайта 12.4 к 8-битному (экранноме значению)
                POP HL                                                          ; восстановление положение спрайта, относительно левой границы видимой области (в пикселях)
                ADD HL, HL
                ADD HL, HL
                ADD HL, HL
                ADD HL, HL
                
.AdjustScrAdr   ; корректировка адреса экрана
                LD A, H
                AND %00000111
                JR Z, .NotShift                                                 ; переход, если сдвиг отсутствует

                ; расчёт адреса таблицы, в зависимости от младших 3 бит
                EXX
                DEC A
                ADD A, A
                ADD A, HIGH Table.Shift
                LD B, A
                EXX

.NotShift       ; корректировка адреса колонки в экранной области
                LD A, H
                RRA
                RRA
                RRA
                AND %00011111
                EXX
                OR L
                LD L, A
                EXX

                SCF
                RET

                display " - Prepare Sprite Unit : \t\t", /A, Prepare, " = busy [ ", /D, $ - Prepare, " bytes  ]"

                endif ; ~ _CORE_MODULE_DRAW_SPRITE_UNIT_PREPARE_SPRITE_
