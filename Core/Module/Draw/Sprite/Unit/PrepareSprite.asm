
                ifndef _CORE_MODULE_DRAW_SPRITE_UNIT_PREPARE_SPRITE_
                define _CORE_MODULE_DRAW_SPRITE_UNIT_PREPARE_SPRITE_

; -----------------------------------------
; подготовка спрайта перед выводм на экран
; In:
;   HL - адрес текущего спрайта
; Out:
; Corrupt:
; Note:
; -----------------------------------------
Prepare:        EX DE, HL
                
                ; инициализация
                LD HL, GameVar.TilemapOffset
                LD C, (HL)                                                      ; X
                INC HL
                LD B, (HL)                                                      ; Y
                
                EXX

.Clip           ; ---------------------------------------------
                ; вертикальный клипинг
                ; ---------------------------------------------

                ; размер спрайта (D - ширина, E - высота) в пикселях
.SpriteSize     EQU $+1
                LD DE, #0000

                ; -----------------------------------------
                ; расчёт положения спрайта по вертикали,
                ; относительно верхней границы видимой области
                ; -----------------------------------------

                ; приведение к форме хранения позиции юнита (12.4)
                ; SOy = FCompositeSpriteInfo.Info.OffsetY << 4
.SOy            EQU $+1
                LD L, #00
                LD A, L
                ADD A, A
                SBC A, A
                LD H, A
                LD A, L
                ADD A, A
                ADD A, A
                ADD A, A
                RL H
                ADD A, A
                RL H
                LD L, A

                ; HL = SOy + FUnit.Position.Y
.PositionY      EQU $+1
                LD BC, #0000
                ADD HL, BC

                ; преобразование высоты спрайта в 16-битное число
                LD B, #00
                LD A, E
                ADD A, A
                ADD A, A
                ADD A, A
                RL B
                ADD A, A
                RL B
                LD C, A

                ; HL = SOy + FUnit.Position.Y - GameVar.TilemapOffset.Y
                LD A, H
                EXX
                SUB B                                                           ; GameVar.TilemapOffset.Y
                EXX
                LD H, A                                                         ; HL - хранит положение спрайта, относительно верхней границы видимой области
                JP P, .BelowTop                                                 ; переход, если спрайт находится ниже верхней границы видимой области

                ; ---------------------------------------------
                ; спрайт выше границы видимой области
                ; ---------------------------------------------

                ; добавить к относительному расположению спрайта по вертикали,
                ; относительно верхней границы видимой области размер спрайта
                ADD HL, BC
                RET NC                                                          ; выход, если при добавлении размера спрайта по вертикали,
                                                                                ; не произошло переполненеи. значение осталось отрицательным
                                                                                ; и спрайт полностью находится выше видимой области

.ClipTop        ; ---------------------------------------------
                ; спрайт урезан верхней частью видимой области
                ; ---------------------------------------------
                ; L - новая высота спрайта
                ; DE - изначальный размер спрайта (D - ширина, E - высота) в пикселях
                ; ---------------------------------------------
                
                ; расчёт количество пропускаемых строк
                LD A, E
                SUB L
                LD E, L                                                         ; сохранение новой высоты спрайта
                
                ; расчёт адреса в таблицы умножение
                LD L, A
                LD A, D
                ADD A, A
                ADD A, A
                XOR L
                AND %01100000
                XOR L

                ; результат:
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 0  | W1 | W0 | R4 | R3 | R2 | R1 | R0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   W1,W0   [6,5]   - ширина спрайта в знакоместах
                ;   R4-R0   [4..0]  - количество пропускаемых строк
                
                LD L, A
                LD H, HIGH Table.MultiplySprite
                LD A, (HL)

                ; корректировка размера пропускаемых данных спрайта,
                ; в зависимости от наличия маски у спрайта
                LD HL, (.SpriteFlags)
                BIT CSIF_OR_XOR_BIT, (HL)
                JR Z, $+3
                ADD A, A

                ; корректировка начального адреса спрайта
                EXX
                ADD A, E
                LD E, A
                ADC A, D
                SUB E
                LD D, A
                EXX

                JR .ClipRow

.BelowTop       ; ---------------------------------------------
                ; спрайт находится ниже верхней границы видимой области
                ; ---------------------------------------------
                ; HL - хранит положение спрайта, относительно верхней границы видимой области
                ; DE - изначальный размер спрайта (D - ширина, E - высота) в пикселях
                ; ---------------------------------------------

                ; проверка нахождения спрайта ниже нижней границы видивой области
                LD A, H
                SUB SCREEN_TILE_X
                RET P                                                           ; выход, если результат положительный, т.к. спрайт находится
                                                                                ; ниже нижней границы видимой области

                ; проверка урезания спрайта нижней частью видимой областью
                LD H, A
                ADD HL, BC
                JR NC, .ClipRow                                                 ; переход, если при добавлении размера спрайта по вертикали,
                                                                                ; не произошло переполненеи. значение осталось отрицательным
                                                                                ; и спрайт полностью находится выше нижней границы видивой области

.ClipBottom     ; ---------------------------------------------
                ; спрайт урезан нижней частью видимой области
                ; ---------------------------------------------
                ; HL - хранит положение спрайта, относительно нижней границы видимой области
                ; DE - изначальный размер спрайта (D - ширина, E - высота) в пикселях
                ; ---------------------------------------------

                ; корректировка высоты спрайта
                LD A, L
                ADD A, E
                LD E, A

.ClipRow        ; ---------------------------------------------
                ; горизонтальный клипинг
                ; ---------------------------------------------


                ; D - FCompositeSpriteInfo.Info.OffsetX (SOx)
                ; B - FCompositeSpriteInfo.Info.Width   (Sx)
.SOx            EQU $+1
                LD L, #00
                ; HL = SOx + FUnit.Position.X - GameVar.TilemapOffset.X
.PositionX      EQU $+1
                LD DE, #0000
.Width          EQU $+1
                LD A, #00

                RET

.SpriteFlags    DB #00                                                          ; флаги спрайта

                display " - Prepare Sprite Unit : \t\t", /A, Prepare, " = busy [ ", /D, $ - Prepare, " bytes  ]"

                endif ; ~ _CORE_MODULE_DRAW_SPRITE_UNIT_PREPARE_SPRITE_
