    
                ifndef _MODULE_INPUT_
                define _MODULE_INPUT_
; -----------------------------------------
; проверка нажатия/отжатия виртуальной клавиши
; In :
;   A  - виртуальная клавиша
;   HL - адрес массива состояний виртуальных клавиш
;   DE - адрес обработчика виртуальных клавиш
; Out :
;   если указанная виртуальная клавиша нажата/отжатия то вызовется обрабочик
;   флаг Z соответствет 0/1 состоянию нажатия/отжатия соответственно
; Corrupt :
;   HL, DE, BC, AF, AF'
; -----------------------------------------
JumpHandlerKey:     CALL Input.CheckKeyState
                    LD A, (HL)
                    JR NZ, .IsReleased
                    OR A
                    JR NZ, .NotProcessed
                    INC (HL)
                    EX DE, HL
                    JP (HL)                                                     ; переход, при нажатии на клавиши
.IsReleased         OR A
                    JR Z, .NotProcessed
                    DEC (HL)
                    EX DE, HL
                    JP (HL)                                                     ; переход, при отжатии на клавиши
.NotProcessed       SCF
                    RET
; -----------------------------------------
; обработчик нажатия/отжатия виртуальной клавиши
; In :
;   DE - адрес обработчика виртуальных клавиш
; Out :
;   если указанная виртуальная клавиша нажата/отжатия то вызовется обрабочик
;   флаг Z соответствет 0/1 состоянию нажатия/отжатия соответственно
;   если обработчик обработал клавишу и не требуется дальнейший проод по виртуальным клавишам, флаг переполнения C должен быть сброшен
; Corrupt :
;   HL, DE, BC, AF, AF'
; -----------------------------------------
JumpKeys:       LD HL, .KeyLastState
                EXX
                LD DE, .ArrayVKNum
                LD B, .Num
; -----------------------------------------
; вызов обработчика виртуальных клавиш при нажатии/отжатии
; In :
;   HL  - адрес массива состояний виртуальных клавиш
;   DE' - адрес массива виртуальных клавиш
;   B'  - количество виртуальных клавиш в массиве
; Out :
;   если указанная виртуальная клавиша нажата/отжатия то вызовется обрабочик
;   флаг Z соответствет 0/1 состоянию нажатия/отжатия соответственно
;   если обработчик обработал клавишу и не требуется дальнейший проод по виртуальным клавишам, флаг переполнения C должен быть сброшен
; Corrupt :
;   HL, DE, BC, AF, AF'
; -----------------------------------------
.Loop           LD A, (DE)
                INC DE
                EX AF, AF'
                LD A, (DE)
                INC DE
                EX AF, AF'
                PUSH DE                                                         ; сохранение, адрес массива виртуальных клавиш
                PUSH BC                                                         ; сохранение, количество опрашиваемых клавиш
                EXX
                PUSH HL                                                         ; сохранение, адрес массива состояний виртуальных клавиш
                PUSH DE                                                         ; сохранение, адрес обработчика виртуальных клавиш
                CALL JumpHandlerKey
                POP DE                                                          ; восстановление, адрес обработчика виртуальных клавиш
                POP HL                                                          ; восстановление, адрес массива состояний виртуальных клавиш
                INC HL
                EXX
                POP BC                                                          ; восстановление, количество опрашиваемых клавиш
                POP DE                                                          ; восстановление, адрес массива виртуальных клавиш
                RET NC                                                          ; выход, если обработчик не желает дальнеший опрос
                DJNZ .Loop
                RET

.KeyLastState   DS NUMBER_KEYS_ID, 0
.ArrayVKNum     DB VK_ENTER,        KEY_ID_MENU                                 ; клавиша "меню/пауза"
                DB VK_CAPS_SHIFT,   KEY_ID_ACCELERATION                         ; клавиша "ускорить"
                DB VK_SYMBOL_SHIFT, KEY_ID_BACK                                 ; клавиша "отмена/назад"
                DB VK_SPACE,        KEY_ID_SELECT                               ; клавиша "выбор"
                DB VK_D,            KEY_ID_RIGHT                                ; клавиша "вправо"
                DB VK_A,            KEY_ID_LEFT                                 ; клавиша "влево"
                DB VK_S,            KEY_ID_DOWN                                 ; клавиша "вниз"
.LastKey        DB VK_W,            KEY_ID_UP                                   ; клавиша "вверх"
.Num            EQU ($-.ArrayVKNum) / 2

                endif ; ~_MODULE_INPUT_
